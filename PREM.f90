
SUBROUTINE PREM(PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
!-------------------------------------------------------------------------------------------


  IMPLICIT NONE

  INTEGER(KIND=4) :: NPR,NPF,NPL
  REAL   (KIND=8) :: PR(NPR),PF(NPF),PL(NPL)
  REAL   (KIND=8) :: GV0(NPR,NPF,NPL),GA0(3,NPR,NPF,NPL),GG0(6,NPR,NPF,NPL)
  
  INTEGER(KIND=4) :: NT,I,J,N
  REAL   (KIND=8) :: A,B,C,SpC(3),SpR(2)
  REAL   (KIND=8),ALLOCATABLE :: T(:)
!-------------------------------------------------------------------------------------------  

                
               
                
  
                !-------------------------------------------------------------                
                !                         Initialization                                                      
                !-------------------------------------------------------------
                GV0(:,:,:)=0.D0;     GA0(:,:,:,:)=0.D0;     GG0(:,:,:,:)=0.D0
                !-------------------------------------------------------------
                
                
                !-------------------------------------------------------------                
                !                         Lower mantle                                                      
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=3480.D3;        SpR(2)=5701.D3
                !-------------------------------------------------------------
                NT=3;        ALLOCATE(T(0:NT));             T(:)=0.D0
                !-------------------------------------------------------------
                T(0)= 7956.5    
                T(1)=-6476.1/6371.D3   
                T(2)= 5528.3/6371.D3**2   
                T(3)=-3080.7/6371.D3**3
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                DEALLOCATE(T)
                !-------------------------------------------------------------
                
                
                !-------------------------------------------------------------                
                !                         Transition zone                                                      
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=5701.D3;        SpR(2)=5771.D3
                !-------------------------------------------------------------
                NT=1;        ALLOCATE(T(0:NT));             T(:)=0.D0
                !-------------------------------------------------------------
                T(0)= 5319.7    
                T(1)=-1483.6/6371.D3   
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=5771.D3;        SpR(2)=5971.D3
                !-------------------------------------------------------------
                T(0)= 11249.4 
                T(1)=-8029.8/6371.D3   
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=5971.D3;        SpR(2)=6151.D3
                !-------------------------------------------------------------
                T(0)= 7108.9    
                T(1)=-3804.5/6371.D3   
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                DEALLOCATE(T)
                !-------------------------------------------------------------
                
                
                !-------------------------------------------------------------                
                !                         LVZ & LID                                                      
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=6151.D3;        SpR(2)=6346.6D3
                !-------------------------------------------------------------
                NT=1;        ALLOCATE(T(0:NT));             T(:)=0.D0
                !-------------------------------------------------------------
                T(0)= 2691.
                T(1)= 692.4/6371.D3   
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                DEALLOCATE(T)
                !-------------------------------------------------------------
                
                
                
                !-------------------------------------------------------------                
                !                         Crust & Ocean                                                     
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=6346.6D3;        SpR(2)=6356.0D3
                !-------------------------------------------------------------
                NT=0;        ALLOCATE(T(0:NT));             T(:)=0.D0
                !-------------------------------------------------------------
                T(0)= 2900.  
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=6356.0D3;        SpR(2)=6368.0D3
                !-------------------------------------------------------------
                T(0)= 2600.  
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                SpC(:)=0.D0;         SpR(1)=6368.0D3;        SpR(2)=6371.0D3
                !-------------------------------------------------------------
                T(0)= 1020.  
                !-------------------------------------------------------------
                CALL Shell(1,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
                !-------------------------------------------------------------
                DEALLOCATE(T)
                !-------------------------------------------------------------

  
                                                                                           
                !-------------------------------------------------------------    
                OPEN(1,FILE='Analytic_PREM.dat',STATUS='UNKNOWN') 
                    !---------------------------------------------------------
                    DO I=1,NPR
                    DO J=1,NPF
                    DO N=1,NPL
                       !------------------------------------------------------
                       WRITE(1,'(15(F30.18,F30.18))') PR(I),PF(J),PL(N),GV0(I,J,N),& 
                                                    & GA0(1,I,J,N),GA0(2,I,J,N),GA0(3,I,J,N),&
                                                    & GG0(1,I,J,N),GG0(2,I,J,N),GG0(3,I,J,N),&
                                                    & GG0(4,I,J,N),GG0(5,I,J,N),GG0(6,I,J,N)
                       !------------------------------------------------------
                    END DO
                    END DO
                    END DO
                    !---------------------------------------------------------
                CLOSE(1)
                !-------------------------------------------------------------
                
                
!-------------------------------------------------------------------------------------------
                                                                                    RETURN
END SUBROUTINE
!===========================================================================================

    
    
    
    
    
!===========================================================================================
!                                      球 壳 解 析 解                                                               
!===========================================================================================


SUBROUTINE Shell(Typ,NT,T,A,B,C,SpC,SpR,PR,PF,PL,NPR,NPF,NPL,GV0,GA0,GG0)
!-------------------------------------------------------------------------------------------
!--Typ[输入参数]：密度函数类型：Typ=1 多项式函数  =2 指数函数
!--NT [输入参数]：T的维度=NT+1
!--T  [输入参数]：球壳密度参数[T],多项式密度:ro=T(NT)*X*NT+...+T(1)*X+T(0)
!--ABC[输入参数]：球壳密度参数[A,B,C],指数密度:ro=A*exp(-B(r-SpR(1)))+C
!--SpC[输入参数]：球心坐标
!--SpR[输入参数]：球壳内、外半径
!--PR [输入参数]：观测区域径向坐标
!--PF [输入参数]：观测区域纬度坐标
!--PL [输入参数]：观测区域经度坐标
!--NR [输入参数]：观测区域径向网格数量
!--NF [输入参数]：观测区域纬度网格数量
!--NL [输入参数]：观测区域经度网格数量
!-------------------------------------------------------------------------------------------


  IMPLICIT NONE

  INTEGER(KIND=4) :: Typ,NT,NPR,NPF,NPL
  REAL   (KIND=8) :: T(0:NT),A,B,C,SpC(3),SpR(2),PR(NPR),PF(NPF),PL(NPL)
  REAL   (KIND=8) :: GV0(NPR,NPF,NPL),GA0(3,NPR,NPF,NPL),GG0(6,NPR,NPF,NPL)
  
  INTEGER(KIND=4) :: I,J,K
  REAL   (KIND=8) :: G0,M,PI,COSF,X(3),L,L2,L3,L5,GAMA
  REAL   (KIND=8) :: GVs,GAs(3),GGs(6)
!-------------------------------------------------------------------------------------------  

                
                
  
                !-------------------------------------------------------------                
                !                         固 定 常 数                                                      
                !-------------------------------------------------------------
                G0=6.674D-11                                     !--引力常数
                !-------------------------------------------------------------
                PI=3.141592653589793238462643383279D0            !--圆周率
                !-------------------------------------------------------------
                IF(Typ==1) THEN
                    !---------------------------------------------------------
                    GAMA=0.D0
                    !---------------------------------------------------------
                    DO I=0,NT
                       !------------------------------------------------------ 
                       GAMA=GAMA+T(I)/(I+3.)*(SpR(2)**(I+3)-SpR(1)**(I+3))
                       !------------------------------------------------------ 
                    END DO
                    !---------------------------------------------------------
                ELSEIF(Typ==2) THEN
                    !---------------------------------------------------------
                    GAMA=A/B**3*((SpR(1)**2*B**2+2.*SpR(1)*B+2.) - (SpR(2)**2*B**2+2.*SpR(2)*B+2.)*EXP(-B*(SpR(2)-SpR(1)))) + C*(SpR(2)**3-SpR(1)**3)/3.D0
                    !---------------------------------------------------------
                ENDIF
                !-------------------------------------------------------------
                
                
                
                !-------------------------------------------------------------                
                !                       球 壳 重 力 场                                                                          
                !-------------------------------------------------------------
                M=4.*PI*G0*GAMA
                !-------------------------------------------------------------
                !OPEN(1,FILE='Shell_Exponential_Ana.dat',STATUS='UNKNOWN')
                   !----------------------------------------------------------
                   DO J=1,NPF
                   DO K=1,NPL
                   DO I=1,NPR
                      !------------------------------------------------------- 
                      COSF=SIN(PF(J))*SIN(SpC(2))+COS(PF(J))*COS(SpC(2))*COS(PL(K)-SpC(3))
                      !------------------------------------------------------- 
                      X(1)=SpC(1)*(COS(PF(J))*SIN(SpC(2))-SIN(PF(J))*COS(SpC(2))*COS(PL(K)-SpC(3)))
                      X(2)=SpC(1)*COS(SpC(2))*SIN(PL(K)-SpC(3))
                      X(3)=SpC(1)*COSF-PR(I)
                      !------------------------------------------------------- 
                      L=SQRT(SUM(X(:)**2));    L2=L**2;    L3=L**3;    L5=L**5
                      !-------------------------------------------------------
                      GVs=M/L                                !--重力位V
                      !-------------------------------------------------------
                      GAs(1)=M*X(1)/L3                       !--重力加速度Gx
                      GAs(2)=M*X(2)/L3                       !--重力加速度Gy
                      GAs(3)=M*X(3)/L3                       !--重力加速度Gz
                      !-------------------------------------------------------
                      GGs(1)=M*(3.*X(1)**2-L2)/L5            !--重力张量Gxx
                      GGs(2)=M*(3.*X(2)**2-L2)/L5            !--重力张量Gyy
                      GGs(3)=M*(3.*X(3)**2-L2)/L5            !--重力张量Gzz
                      GGs(4)=M*(3.*X(1)*X(2))/L5             !--重力张量Gxy
                      GGs(5)=M*(3.*X(1)*X(3))/L5             !--重力张量Gxz
                      GGs(6)=M*(3.*X(2)*X(3))/L5             !--重力张量Gyz
                      !-------------------------------------------------------
                      GV0(I,J,K)=GV0(I,J,K)+GVs 
                      GA0(:,I,J,K)=GA0(:,I,J,K)+GAs(:)
                      GG0(:,I,J,K)=GG0(:,I,J,K)+GGs(:)
                      !-------------------------------------------------------
                      !WRITE(1,'(15(F30.18,F30.18))') PR(I),PL(K),PF(J),&
                      !                                 & GVs,GAs(1),GAs(2),GAs(3), &
                      !                                 & GGs(1),GGs(2),GGs(3), &
                      !                                 & GGs(4),GGs(5),GGs(6)
                      !-------------------------------------------------------
                   END DO
                   END DO
                   END DO
                   !----------------------------------------------------------
                !CLOSE(1)
                !-------------------------------------------------------------
  

  
!-------------------------------------------------------------------------------------------
                                                                                    RETURN
END SUBROUTINE
!===========================================================================================