
SUBROUTINE Tesseroid(NP,PR0,PF0,PL0,SRm,SFm,SLm,dSR,dSF,dSL,IV,IA,IG)
!-------------------------------------------------------------------------------------------
                                                                                USE Const
                                                                                USE Gauss
  IMPLICIT NONE

  INTEGER(KIND=4) :: NP
  REAL   (KIND=8) :: PR0,PF0,PL0,SRm,SFm,SLm,dSR,dSF,dSL,IV(0:NP),IA(0:NP,3),IG(0:NP,6)
  
  INTEGER(KIND=4) :: JJ,KK,I0,NN
  REAL   (KIND=8) :: SFnew(NG1),SLnew(NG2),SRnew(2),SINP,COSP,TANP,SINS,COSS,CF,CK,CC0
  REAL   (KIND=8) :: L(2),L3(2),BETA(2),Id1,Id2,dCF,VG(0:NP,6),PR02,PR0F,DR(2),CFD
  REAL   (KIND=8) :: EPS(0:NP+2,2),ETA(0:NP+3,2),KXI(0:NP+4,2)
  REAL   (KIND=8) :: ETAX(0:NP),KXIX(0:NP),ETAY(0:NP),KXIY(0:NP)
!-------------------------------------------------------------------------------------------  



                                               
                !-------------------------------------------------------------
                SRnew(1)=SRm-0.5*dSR
                SRnew(2)=SRm+0.5*dSR
                !-------------------------------------------------------------
                SFnew(:)=SFm+0.5*dSF*TP1(:)
                SLnew(:)=SLm+0.5*dSL*TP2(:)-PL0
                !-------------------------------------------------------------
                SINP=SIN(PF0)
                COSP=COS(PF0)
                TANP=TAN(PF0)
                !-------------------------------------------------------------
                PR02=PR0**2
                !-------------------------------------------------------------
                IV(:)=0.D0;  IA(:,:)=0.D0;  VG(:,:)=0.D0;  I0=0
                !-------------------------------------------------------------
                DO JJ=1,NG1
                   !---------------------------------------------------------- 
                   SINS=SIN(SFnew(JJ))
                   COSS=COS(SFnew(JJ))
                   !---------------------------------------------------------- 
                DO KK=1,NG2
                   !----------------------------------------------------------  
                   Id1=COSS*COS(SLnew(KK))
                   Id2=COSS*SIN(SLnew(KK))
                   !---------------------------------------------------------- 
                   CF =SINP*SINS+COSP*Id1
                   dCF=COSP*SINS-SINP*Id1
                   !---------------------------------------------------------- 
                   L(:)=PR0*SQRT(1.+(SRnew(:)/PR0 )*((SRnew(:)/PR0)-2.*CF))
                   !---------------------------------------------------------- 
                   L3(:)=L(:)**3
                   PR0F=PR0*CF
                   !---------------------------------------------------------- 
                   IF(ABS(CF-1.D0)<err) THEN
                       !------------------------------------------------------ 
                       DR(:)=SRnew(:)-PR0
                       !------------------------------------------------------ 
                       EPS(0,:)=-LOG(ABS(DR(:)))
                       ETA(0,:)=0.5D0/DR(:)**2
                       KXI(0,:)=ETA(0,:)*ETA(0,:)
                       !------------------------------------------------------ 
                   ELSE IF(ABS(CF+1.D0)<err) THEN
                       !------------------------------------------------------ 
                       DR(:)=SRnew(:)+PR0
                       !------------------------------------------------------ 
                       EPS(0,:)=LOG(DR(:))
                       ETA(0,:)=-0.5D0/DR(:)**2
                       KXI(0,:)=-ETA(0,:)*ETA(0,:)
                       !------------------------------------------------------ 
                   ELSE
                       !------------------------------------------------------ 
                       DR(:)=(SRnew(:)/PR0-CF)/PR0
                       CFD=1.-CF**2
                       !------------------------------------------------------ 
                       EPS(0,:)=LOG(L(:)+SRnew(:)-PR0*CF)
                       ETA(0,:)=DR(:)/CFD/L(:)
                       KXI(0,:)=ETA(0,:)/3.*(1./L(:)**2+ETA(0,:)/(SRnew(:)/PR0-CF)*2.*(L(:)/PR0))
                       !------------------------------------------------------ 
                   END IF
                   !---------------------------------------------------------- 
                   EPS(1,:)=L(:)+PR0F*EPS(0,:)
                   !---------------------------------------------------------- 
                   DO NN=2,NP+2
                      !------------------------------------------------------- 
                      EPS(NN,:)=(SRnew(:)**(NN-1)*L(:)+PR0*((2.*NN-1.)*CF*EPS(NN-1,:)-(NN-1.)*PR0*EPS(NN-2,:)))/NN 
                      !------------------------------------------------------- 
                   END DO
                   !---------------------------------------------------------- 
                   ETA(1,:)=-1./L(:)+PR0F*ETA(0,:)
                   !---------------------------------------------------------- 
                   ETA(2,:)=-SRnew(:)/L(:)+PR0F*ETA(1,:)+EPS(0,:)
                   !---------------------------------------------------------- 
                   DO NN=3,NP+3
                      !------------------------------------------------------- 
                      ETA(NN,:)=(SRnew(:)**(NN-1)/L(:)+PR0*((2.*NN-3.)*CF*ETA(NN-1,:)-(NN-1)*PR0*ETA(NN-2,:)))/(NN-2.)
                      !------------------------------------------------------- 
                   END DO
                   !---------------------------------------------------------- 
                   ETAX(:)=ETA(3:NP+3,2)-ETA(3:NP+3,1)
                   ETAY(:)=ETA(2:NP+2,2)-ETA(2:NP+2,1)
                   !---------------------------------------------------------- 
                   KXI(1,:)=-(1./L3(:)-3.*PR0F*KXI(0,:))/3.D0
                   !---------------------------------------------------------- 
                   DO NN=2,3 
                       !------------------------------------------------------
                       KXI(NN,:)=(SRnew(:)**(NN-1.)/L3(:)+PR0*((2.*NN-5.)*CF*KXI(NN-1,:)-(NN-1.)*PR0*KXI(NN-2,:)))/(NN-4.)
                       !------------------------------------------------------
                   END DO
                   !---------------------------------------------------------- 
                   KXI(4,:)=-SRnew(:)**3/3./L3(:)+PR0F*KXI(3,:)+ETA(2,:)
                   !---------------------------------------------------------- 
                   DO NN=5,NP+4
                       !------------------------------------------------------
                       KXI(NN,:)=(SRnew(:)**(NN-1.)/L3(:)+PR0*((2.*NN-5.)*CF*KXI(NN-1,:)-(NN-1.)*PR0*KXI(NN-2,:)))/(NN-4.)
                       !------------------------------------------------------
                   END DO
                   !---------------------------------------------------------- 
                   KXIX(:)=KXI(4:NP+4,2)-KXI(4:NP+4,1)
                   KXIY(:)=KXI(3:NP+3,2)-KXI(3:NP+3,1)
                   !---------------------------------------------------------- 
                   I0=I0+1;   CK=COSS*APX(I0)
                   !---------------------------------------------------------- 
                   !--V
                   IV(:)=IV(:)+CK*(EPS(2:NP+2,2)-EPS(2:NP+2,1))
                   !---------------------------------------------------------- 
                   !--Gx,Gy,Gz
                   IA(:,1)=IA(:,1)+CK*dCF*ETAX(:); 
                   IA(:,2)=IA(:,2)+CK*Id2*ETAX(:);
                   IA(:,3)=IA(:,3)+CK*(-PR0*ETAY(:)+CF*ETAX(:))
                   !---------------------------------------------------------- 
                   !--Gxx,Gyy,Gzz,Gxy,Gxz,Gyz
                   VG(:,1)=VG(:,1)+CK*(-CF*ETAX(:)/PR0+3.*dCF**2*KXIX(:))
                   VG(:,2)=VG(:,2)+CK*(-Id1*ETAX(:)/PR0/COSP+3.*Id2**2*KXIX(:))
                   VG(:,3)=VG(:,3)+CK*(2.*ETAY(:)-3.*(1.-CF**2)*KXIX(:))
                   VG(:,4)=VG(:,4)+CK*Id2*(-TANP*ETAX(:)/PR0+3.*dCF*KXIX(:))
                   !---------------------------------------------------------- 
                   ETAX(:)=(ETAX(:)/PR0-3.*(PR0*KXIY(:)-CF*KXIX(:)))
                   !---------------------------------------------------------- 
                   VG(:,5)=VG(:,5)+CK*dCF*ETAX(:)
                   VG(:,6)=VG(:,6)+CK*Id2*ETAX(:)
                   !---------------------------------------------------------- 
                END DO
                END DO
                !-------------------------------------------------------------
                !--Gxx,Gyy,Gzz,Gxy,Gxz,Gyz
                IG(:,1)=VG(:,1)+IA(:,3)/PR0
                IG(:,2)=VG(:,2)+(IA(:,3)-TANP*IA(:,1))/PR0
                IG(:,3)=VG(:,3)
                IG(:,4)=VG(:,4)+TANP*IA(:,2)/PR0
                IG(:,5)=VG(:,5)-IA(:,1)/PR0
                IG(:,6)=VG(:,6)-IA(:,2)/PR0
                !-------------------------------------------------------------
                CC0=dSF*dSL
                !-------------------------------------------------------------
                IV(:)=IV(:)*CC0
                IA(:,:)=IA(:,:)*CC0
                IG(:,:)=IG(:,:)*CC0
                !-------------------------------------------------------------
                

                
!-------------------------------------------------------------------------------------------
                                                                                    RETURN
END SUBROUTINE
!===========================================================================================
    
    
    